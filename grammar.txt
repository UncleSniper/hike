utterance		::= toplevel*
toplevel		::= goal
					| 'artifact' artifact
					| setvar
					| setvardef
					| include

goal			::= 'goal' NAME (action | '{' goal_body '}')
goal_body		::= ('label' STRING)? action+
setvar			::= 'set' NAME (STRING | INT)
setvardef		::= 'setdef' NAME (STRING | INT)
include			::= 'include' 'ifExists'? STRING

action			::= attain
					| require
					| delete
attain			::= 'attain' NAME
require			::= 'require' artifact_ref
delete			::= 'delete' (STRING | x_artifact_ref)

artifact_ref	::= STRING | artifact
x_artifact_ref	::= 'artifact' STRING | artifact
artifact		::= file_artifact
					| group_artifact
					| pipeline
					| tree_artifact
					| split_artifact
file_artifact	::= 'file' STRING (STRING | '{' file_body '}')
file_body		::= STRING ('name' STRING)? transform?
group_artifact	::= 'artifacts' STRING '{' group_body '}'
group_body		::= 'name' STRING artifact_ref*
pipeline		::= 'pipeline' '{' pipeline_opt* artifact_set ('merge'? artifact_fact)* '}'
pipeline_opt	::= 'key' STRING
					| 'name' STRING
					| 'base' STRING
tree_artifact	::= 'tree' STRING (STRING | '{' tree_body '}')
tree_body		::= STRING tree_opt* file_filter*
tree_opt		::= 'name' STRING
					| 'noCache'
split_artifact	::= 'split' STRING? '{' artifact_ref artifact_ref '}'

transform		::= exec_transform
					| copy_transform
					| zip_transform
exec_transform	::= 'exec' STRING '{' command_word+ exec_option* x_artifact_ref* '}'
command_word	::= STRING
					| 'source'
					| 'dest'
					| '{' command_word+ '}'
exec_option		::= 'loud'
					| 'suffixIsDestination'
copy_transform	::= 'copy' (artifact_ref | '{' copy_xform_body '}')
copy_xform_body	::= artifact_ref+ copy_option*
copy_option		::= 'rebaseFrom' STRING
					| 'toDirectory'
zip_transform	::= 'zip' STRING? '{' zip_piece* '}'
zip_piece		::= 'piece' '{' zip_piece_opt* artifact_ref* '}'
zip_piece_opt	::= 'from' STRING
					| 'to' STRING

artifact_set	::= artifact
					| artifact_each
					| scandir
artifact_each	::= 'each' '{' artifact_set* '}'
scandir			::= 'scandir' (STRING | '{' scandir_bdy '}')
scandir_bdy		::= STRING scandir_opt* file_filter*
scandir_opt		::= 'key' STRING
					| 'name' STRING
					| 'base' STRING
file_filter		::= 'files'
					| 'directories'
					| 'wildcard' STRING

artifact_fact	::= static_file
					| regex_file
static_file		::= 'file' (STRING | '{' static_file_bdy '}')
static_file_bdy	::= STRING static_file_opt* transform_fact?
static_file_opt	::= 'key' STRING
					| 'name' STRING
					| 'base' STRING
regex_file		::= 'regex' (STRING STRING | '{' regex_file_bdy '}')
regex_file_bdy	::= STRING STRING regex_file_opt* transform_fact?
regex_file_opt	::= 'key' STRING
					| 'name' STRING
					| 'base' STRING
					| 'rebaseFrom' STRING
					| 'rebaseTo' STRING

transform_fact	::= exec_xfrm_fact
					| copy_xfrm_fact
exec_xfrm_fact	::= 'exec' STRING '{' command_word+ exec_option* '}'
copy_xfrm_fact	::= 'copy' ('{' copy_option* '}')?
